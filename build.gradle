plugins {
    id 'java'
    id 'application'
}

group = 'io.loom'
version = project.findProperty('app.version')

repositories {
    mavenCentral()
}

processResources {
    filesMatching('app.properties') {
        expand(version: project.findProperty('app.version'))
    }
}

def junitVersion = '5.10.2'
def lombokVersion = '1.18.42'
def flatLafVersion = '3.7'
def javaOpts = [
        '-Xms32m',
        '-Xmx384m',
        '-Xss1m',

        // Metaspace settings
        '-XX:MaxMetaspaceSize=96m',
        '-XX:MetaspaceSize=24m',
        '-XX:CompressedClassSpaceSize=12m',

        '-XX:+UseG1GC',
        '-XX:MinHeapFreeRatio=5',
        '-XX:MaxHeapFreeRatio=25',
        '-XX:GCTimeRatio=19',
        '-XX:G1HeapRegionSize=1m',
        '-XX:InitiatingHeapOccupancyPercent=35',

        // String optimization
        '-XX:+UseStringDeduplication',
        '-XX:StringTableSize=60013',

        // Compilation settings
        '-XX:+TieredCompilation',
        '-XX:TieredStopAtLevel=1',

        // Experimental optimizations
        '-XX:+OptimizeStringConcat',
        '-XX:+UseCompressedOops',
        '-XX:+UseCompressedClassPointers',

        // Graphics settings
        '-Dsun.java2d.noddraw=true',
        '-Dsun.java2d.d3d=false',
        '-Dsun.java2d.opengl=false',

        // Module access
        '--add-exports=java.base/java.lang=ALL-UNNAMED',
        '--add-exports=java.desktop/sun.awt=ALL-UNNAMED',
        '--add-exports=java.desktop/sun.java2d=ALL-UNNAMED',
        '--add-opens=java.desktop/sun.awt=ALL-UNNAMED',
        '--add-opens=java.desktop/sun.lwawt=ALL-UNNAMED',
        '--add-opens=java.desktop/sun.lwawt.macosx=ALL-UNNAMED'
]

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

application {
    mainClass = 'io.loom.app.LoomApplication'
    applicationName = 'Loom'
    applicationDefaultJvmArgs = javaOpts
}

dependencies {
    implementation 'me.friwi:jcefmaven:141.0.10'
    implementation 'com.github.weisj:jsvg:2.0.0'
    implementation "com.formdev:flatlaf:$flatLafVersion"
    implementation "com.formdev:flatlaf-extras:$flatLafVersion"
    implementation 'org.slf4j:slf4j-api:2.0.17'
    implementation 'ch.qos.logback:logback-classic:1.5.27'
    implementation 'org.apache.commons:commons-collections4:4.5.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.21.0'

    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'com.github.kwhat:jnativehook:2.2.2'

    compileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"

    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

tasks.register('createInstaller', Exec) {
    dependsOn installDist

    group = 'distribution'
    description = 'Builds a standalone installer using jpackage'

    def javaHome = System.getProperty("java.home")
    def jpackage = "${javaHome}/bin/jpackage"
    if (org.gradle.internal.os.OperatingSystem.current().isWindows()) {
        jpackage += ".exe"
    }

    def installerType = "deb"
    def iconPath = "src/main/resources/app-icons/icon.png"
    def winOptions = []
    def macOptions = []
    def appVersion = version

    if (org.gradle.internal.os.OperatingSystem.current().isWindows()) {
        installerType = "exe"
        iconPath = "src/main/resources/app-icons/icon.ico"
        winOptions = [
                '--win-dir-chooser',
                '--win-menu',
                '--win-shortcut-prompt',
                '--win-per-user-install',
                '--win-upgrade-uuid', 'f3c4e5d6-a7b8-9c0d-1e2f-3a4b5c6d7e8f'
        ]
    } else if (org.gradle.internal.os.OperatingSystem.current().isMacOsX()) {
        installerType = "dmg"
        iconPath = "src/main/resources/app-icons/icon.icns"

        javaOpts += [
                '-Dsun.java2d.opengl=true',
                '-Dsun.java2d.renderer=sun.java2d.marlin.MarlinRenderingEngine'
        ]

        macOptions = [
                '--mac-package-identifier', 'io.loom.app',
                '--mac-package-name', 'Loom',
                '--mac-entitlements', 'src/main/resources/mac/entitlements.plist'
        ]

        if (version.toString().startsWith("0.")) {
            appVersion = "1" + version.toString().substring(1)
            println "MacOS requires version >= 1. Patching installer version to: $appVersion"
        }
    }

    commandLine jpackage,
            '--type', installerType,
            '--dest', 'build/installer',
            '--name', 'Loom',
            '--vendor', 'OldValencia',
            '--app-version', appVersion,
            '--icon', iconPath,
            '--input', 'build/install/Loom/lib',
            '--main-jar', "Loom-${version}.jar",
            '--main-class', application.mainClass.get(),
            '--add-modules', 'java.desktop,jdk.unsupported,java.logging,java.naming,java.management,jdk.crypto.ec',
    *winOptions,
    *macOptions,

    *javaOpts.collectMany { ['--java-options', it] }

    doFirst {
        println "Building installer using: $jpackage"
        file('build/installer').mkdirs()
    }
}
