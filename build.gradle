plugins {
    id 'java'
    id 'application'
}

group = 'io.loom'
version = '0.0.6'

repositories {
    mavenCentral()
}

def junitVersion = '5.10.2'
def lombokVersion = '1.18.42'
def flatLafVersion = '3.7'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

application {
    mainClass = 'io.loom.app.LoomApplication'
    applicationName = 'Loom'
}

dependencies {
    implementation 'me.friwi:jcefmaven:141.0.10'
    implementation 'org.yaml:snakeyaml:2.2'
    implementation 'com.github.weisj:jsvg:2.0.0'
    implementation "com.formdev:flatlaf:$flatLafVersion"
    implementation "com.formdev:flatlaf-extras:$flatLafVersion"
    implementation 'org.slf4j:slf4j-api:2.0.17'
    implementation 'ch.qos.logback:logback-classic:1.5.27'
    implementation 'org.apache.commons:commons-collections4:4.5.0'

    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'com.github.kwhat:jnativehook:2.2.2'

    compileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"

    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

tasks.register('createInstaller', Exec) {
    dependsOn installDist

    group = 'distribution'
    description = 'Builds a standalone installer using jpackage'

    def javaHome = System.getProperty("java.home")
    def jpackage = "${javaHome}/bin/jpackage"
    if (org.gradle.internal.os.OperatingSystem.current().isWindows()) {
        jpackage += ".exe"
    }

    def javaOpts = [
            '-Xms16m',
            '-Xmx256m',
            '-Xss128k',

            '-XX:MaxMetaspaceSize=128m',
            '-XX:MetaspaceSize=32m',
            '-XX:CompressedClassSpaceSize=16m',

            '-XX:+UseSerialGC',
            '-XX:MinHeapFreeRatio=5',
            '-XX:MaxHeapFreeRatio=10',
            '-XX:GCTimeRatio=19',

            '-XX:+UseStringDeduplication',
            '-XX:StringTableSize=60013',

            '-XX:+TieredCompilation',
            '-XX:TieredStopAtLevel=1',
            '-XX:ReservedCodeCacheSize=32m',

            // experimental
            '-XX:+UnlockExperimentalVMOptions',
            '-XX:+UseContainerSupport',
            '-XX:+OptimizeStringConcat',
            '-XX:+UseCompressedOops',

            '-XX:-UsePerfData',
            '-XX:+AlwaysPreTouch',

            '-Dsun.java2d.d3d=false',
            '-Dsun.java2d.ddoffscreen=false',
            '-Dsun.java2d.noddraw=true',
            '-Dsun.java2d.uiScale=1.0',

            '--add-exports=java.base/java.lang=ALL-UNNAMED',
            '--add-exports=java.desktop/sun.awt=ALL-UNNAMED',
            '--add-exports=java.desktop/sun.java2d=ALL-UNNAMED',
            '--add-opens=java.desktop/sun.awt=ALL-UNNAMED',
            '--add-opens=java.desktop/sun.lwawt=ALL-UNNAMED',
            '--add-opens=java.desktop/sun.lwawt.macosx=ALL-UNNAMED'
    ]

    def installerType = "deb"
    def iconPath = "src/main/resources/app-icons/icon.png"
    def winOptions = []

    if (org.gradle.internal.os.OperatingSystem.current().isWindows()) {
        installerType = "exe"
        iconPath = "src/main/resources/app-icons/icon.ico"
        winOptions = [
                '--win-dir-chooser',
                '--win-menu',
                '--win-shortcut-prompt',
                '--win-per-user-install',
                '--win-upgrade-uuid', 'f3c4e5d6-a7b8-9c0d-1e2f-3a4b5c6d7e8f'
        ]
    } else if (org.gradle.internal.os.OperatingSystem.current().isMacOsX()) {
        installerType = "dmg"
        iconPath = "src/main/resources/app-icons/icon.icns"
    }

    commandLine jpackage,
            '--type', installerType,
            '--dest', 'build/installer',
            '--name', 'Loom',
            '--vendor', 'OldValencia',
            '--app-version', version,
            '--icon', iconPath,
            '--input', 'build/install/Loom/lib',
            '--main-jar', "Loom-${version}.jar",
            '--main-class', application.mainClass.get(),
            '--add-modules', 'java.desktop,jdk.unsupported,java.logging,java.naming,java.management,jdk.crypto.ec',
    *winOptions,

    *javaOpts.collectMany { ['--java-options', it] }

    doFirst {
        println "Building installer using: $jpackage"
        file('build/installer').mkdirs()
    }
}