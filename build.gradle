plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0'
}

group = 'io.loom'
version = project.findProperty('app.version')

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

processResources {
    filesMatching('app.properties') {
        expand(project.properties)
    }
}

javafx {
    version = "21.0.2"
    modules = [ 'javafx.controls', 'javafx.web', 'javafx.swing' ]
}

def junitVersion = '5.10.2'
def lombokVersion = '1.18.42'
def flatLafVersion = '3.7'

// Mirrors AppPreferences.DIR logic exactly
def userHome = System.getProperty("user.home")
def isMacBuild = org.gradle.internal.os.OperatingSystem.current().isMacOsX()
def loomDataDir = isMacBuild
        ? "${userHome}/Library/Application Support/Loom"
        : "${userHome}/.loom"

def javaOpts = [
        '-Xms24m',
        '-Xmx96m',
        '-Xss512k',

        '-XX:MaxMetaspaceSize=64m',
        '-XX:MetaspaceSize=16m',
        '-XX:CompressedClassSpaceSize=8m',

        '-XX:+UseG1GC',
        '-XX:MinHeapFreeRatio=5',
        '-XX:MaxHeapFreeRatio=15',
        '-XX:GCTimeRatio=4',
        '-XX:G1HeapRegionSize=1m',
        '-XX:InitiatingHeapOccupancyPercent=25',
        '-XX:G1ReservePercent=5',

        '-XX:+UseStringDeduplication',
        '-XX:StringTableSize=40009',

        '-XX:+TieredCompilation',
        '-XX:+OptimizeStringConcat',
        '-XX:+UseCompressedOops',
        '-XX:+UseCompressedClassPointers',

        '-XX:ReservedCodeCacheSize=32m',
        '-XX:InitialCodeCacheSize=4m',

        // JVM silently ignores this if the file doesn't exist yet
        "-XX:SharedArchiveFile=${loomDataDir}/loom-classes.jsa",

        '--add-exports=java.base/java.lang=ALL-UNNAMED',
        '--add-exports=java.desktop/sun.awt=ALL-UNNAMED',
        '--add-exports=java.desktop/sun.java2d=ALL-UNNAMED',
        '--add-opens=java.desktop/sun.awt=ALL-UNNAMED',
        '--add-opens=java.desktop/sun.lwawt=ALL-UNNAMED',
        '--add-opens=java.desktop/sun.lwawt.macosx=ALL-UNNAMED',
]

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

application {
    mainClass = 'io.loom.app.LoomApplication'
    applicationName = 'Loom'
    applicationDefaultJvmArgs = javaOpts
}

dependencies {
    implementation 'com.github.webview:webview_java:1.3.0'

    implementation 'net.java.dev.jna:jna:5.14.0'
    implementation 'net.java.dev.jna:jna-platform:5.14.0'

    implementation 'com.github.weisj:jsvg:2.0.0'
    implementation "com.formdev:flatlaf:$flatLafVersion"
    implementation "com.formdev:flatlaf-extras:$flatLafVersion"
    implementation 'org.slf4j:slf4j-api:2.0.17'
    implementation 'ch.qos.logback:logback-classic:1.5.27'
    implementation 'org.apache.commons:commons-collections4:4.5.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.21.0'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'com.github.kwhat:jnativehook:2.2.2'

    compileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"

    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

// Run once after install: ./gradlew generateCdsArchive
tasks.register('generateCdsArchive', JavaExec) {
    dependsOn installDist
    group = 'build'
    description = 'Generates AppCDS archive â€” run once, saves ~30 MB RSS on every launch'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = application.mainClass
    jvmArgs = [
            "-XX:ArchiveClassesAtExit=${loomDataDir}/loom-classes.jsa",
            '-Xshare:off',
    ]
}

tasks.register('createInstaller', Exec) {
    dependsOn installDist

    group = 'distribution'
    description = 'Builds a standalone installer using jpackage'

    def javaHome = System.getProperty("java.home")
    def jpackage = "${javaHome}/bin/jpackage"
    if (org.gradle.internal.os.OperatingSystem.current().isWindows()) {
        jpackage += ".exe"
    }

    def installerType = "deb"
    def iconPath = "src/main/resources/app-icons/icon.png"
    def winOptions = []
    def macOptions = []
    def appVersion = version

    if (org.gradle.internal.os.OperatingSystem.current().isWindows()) {
        installerType = "exe"
        iconPath = "src/main/resources/app-icons/icon.ico"
        winOptions = [
                '--win-dir-chooser',
                '--win-menu',
                '--win-shortcut-prompt',
                '--win-per-user-install',
                '--win-upgrade-uuid', 'f3c4e5d6-a7b8-9c0d-1e2f-3a4b5c6d7e8f'
        ]
    } else if (org.gradle.internal.os.OperatingSystem.current().isMacOsX()) {
        installerType = "dmg"
        iconPath = "src/main/resources/app-icons/icon.icns"

        macOptions = [
                '--mac-package-identifier', 'io.loom.app',
                '--mac-package-name', 'Loom',
                '--mac-entitlements', 'src/main/resources/mac/entitlements.plist',
                '--resource-dir', 'src/main/resources/mac'
        ]

        if (version.toString().startsWith("0.")) {
            appVersion = "1" + version.toString().substring(1)
            println "MacOS requires version >= 1. Patching installer version to: $appVersion"
        }
    }

    commandLine jpackage,
            '--type', installerType,
            '--dest', 'build/installer',
            '--name', 'Loom',
            '--vendor', 'OldValencia',
            '--app-version', appVersion,
            '--icon', iconPath,
            '--input', 'build/install/Loom/lib',
            '--main-jar', "Loom-${version}.jar",
            '--main-class', application.mainClass.get(),
            '--add-modules', 'java.desktop,jdk.unsupported,java.logging,java.naming,java.management,jdk.crypto.ec',
    *winOptions,
    *macOptions,
    *javaOpts.collectMany { ['--java-options', it] }

    doFirst {
        println "Building installer using: $jpackage"
        file('build/installer').mkdirs()
    }
}
